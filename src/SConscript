# -*- mode: python; coding: utf-8; -*-

#  Codezero -- a microkernel for embedded systems.
#
#  Copyright Â© 2009  B Labs Ltd
#
#  This program is free software: you can redistribute it and/or modify it under the terms of the GNU
#  General Public License as published by the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
#  the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
#  License for more details.
#
#  You should have received a copy of the GNU General Public License along with this program.  If not, see
#  <http://www.gnu.org/licenses/>.
#
#  Author: Russel Winder

import os.path

Import('environment')

sources = \
        Glob('api/*.[cS]') + \
        Glob('generic/*.[cS]') + \
        Glob('lib/*.[cS]') + \
        Glob('arch/' + environment['ARCH'] + '/*.[cS]') + \
        Glob('arch/' + environment['ARCH'] + '/' + environment['SUBARCH'] +'/*.[cS]') + \
        Glob('glue/' + environment['ARCH'] + '/*.[cS]') + \
        Glob('platform/' + environment['PLATFORM'] + '/*.[cS]')

for item in environment['DRIVER'] :
    path = 'drivers/' + item
    if not os.path.isdir(path):
        feature , device = item.split ( '/' )
        raise ValueError, 'Driver ' + device + ' for ' + feature + ' not available.'
    sources += Glob(path + '/*.[cS]')

objects = environment.Object(sources)
Depends(objects, environment['configFiles'])

startAxf = environment.Program('start.axf', objects)
Depends(startAxf, environment['configFiles'])

Return('startAxf')
